*** Calculus independent equations of CINNI

fmod CINNI is
  protecting NAT .
  protecting QID .

  *** Channel definition
  sort Chan .
  op _{_} : Qid Nat -> Chan [prec 1] .

  sort Subst .
  op [_:=_] : Qid Chan -> Subst .
  op [shift_] : Qid -> Subst .
  op [lift__] : Qid Subst -> Subst .
  op __ : Subst Chan -> Chan .

  vars x y : Qid .
  var n : Nat .
  var a : Chan .
  var S : Subst .

  eq [x := a] x{0} = a .
  eq [x := a] x{s(n)} = x{n} .
  ceq [x := a] y{n} = y{n} if x =/= y .

  eq [shift x] x{n} = x{s(n)} .
  ceq [shift x] y{n} = y{n} if x =/= y .

  eq [lift x S] x{0} = x{0} .
  eq [lift x S] x{s(n)} = [shift x] S x{n} .
  ceq [lift x S] y{n} = [shift x] S y{n} if x =/= y .
endfm
