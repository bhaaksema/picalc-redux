fmod NAMESET is
  including CINNI .

  sort Nameset .
  subsort Name < Nameset .
  op mt : -> Nameset [ctor] .
  op __ : Nameset Nameset -> Nameset [assoc comm id: mt] .
  op _in_ : Name Nameset -> Bool .
  op _subset_ : Nameset Nameset -> Bool .
  op __ : Subst Nameset -> Nameset .

  *** eliminate duplicate elements
  vars a b : Name .
  vars E F : Nameset .
  eq a a E = a E .

  *** set membership
  eq a in mt = false .
  eq a in b E = a == b or a in E .

  *** subset relation .
  eq mt subset E = true .
  eq a E subset F = a in F and E subset F .

  *** substitution
  var S : Subst .
  eq S mt = mt .
  eq S (a E) = (S a) (S E) .

  *** generate fresh qids wrt a given set of names
  var x y : Qid .
  var N : Nat .
  op _usedin_ : Qid Nameset -> Bool .
  eq x usedin mt = false .
  eq x usedin y{N} E = x == y or x usedin E .

  *** reduces the index of names with the given qid
  *** as tag, removing the name if its index is 0
  var n : Nat .
  op down : Qid Nameset -> Nameset .
  eq down(x, mt) = mt .
  eq down(x, y{0} E) = if x == y then down(x, E)
    else y{0} down (x, E) fi .
  eq down(x, y{s(n)} E) = if x == y then y{n} down(x, E)
    else y{s(n)} down(x, E) fi .
endfm
